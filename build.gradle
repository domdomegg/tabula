// Configuration dependencies, e.g. for buildscript plugins
buildscript {
    repositories {
        mavenCentral()
        maven { url "https://maven.eveoh.nl/content/repositories/releases" }
        maven { url "http://www.terracotta.org/download/reflector/releases" }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenLocal()
    }

    dependencies {
        classpath "gradle.plugin.org.zeroturnaround:gradle-jrebel-plugin:1.1.8"
    }
}

plugins {
    id 'net.saliman.properties' version '1.4.6'
}

// Some properties that we use to resolve dependencies etc.
ext {
    scalaVersion = "2.11.11"
    scalaMajorVersion = "2.11"
    springVersion = "4.3.14.RELEASE"
    hibernateVersion = "4.3.11.Final"
    jacksonVersion = "2.6.7"
    elasticsearchVersion = "2.1.1"
    dispatchVersion = "0.8.10"
    poiVersion = "3.17"
    tilesVersion = "3.0.8"
    jcloudsVersion = "2.0.2"
    warwickUtilsVersion = "20171201"
    surefireVersion = "2.19.1"
}

allprojects {
    apply plugin: 'idea'
    idea {
        module {
            outputDir file('build/classes/scala/main')
            testOutputDir file('build/classes/scala/test')
        }
    }
}

// Configuration that applies to all subprojects (including common, so nothing war-specific here)
subprojects {
    apply plugin: 'scala'
    apply plugin: "org.zeroturnaround.gradle.jrebel"

    // TAB-5441 - additional jrebel config to include language specific build dirs
    rebel {
        classpath {
            // the default element
            resource {}

            resource {
                directory = "build/classes/java/main"
                includes = ["**/*"]
                excludes = ["*.java", "*.properties"]
            }

            resource {
                directory = "build/classes/scala/main"
                includes = ["**/*"]
                excludes = ["*.scala", "*.properties"]
            }
        }
    }

    // Target Java 8
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    sourceSets {
        // We want joint Scala/Java compilation, so remove all the Java source directories and have them compiled
        // by the scala compiler instead
        main {
            java.srcDirs = []
            scala.srcDirs += 'src/main/java'

            // Allow resources to be defined directly in the source directory
            resources.srcDirs += 'src/main/scala'
        }
        test {
            java.srcDirs = []
            scala.srcDirs += 'src/test/java'

            // Allow resources to be defined directly in the source directory
            resources.srcDirs += 'src/test/scala'

            // Same configuration as the WAR classloader
            resources.srcDirs += 'src/main/webapp'

            // Make compileOnly dependencies available to tests
            compileClasspath += configurations.compileOnly
            runtimeClasspath += configurations.compileOnly
        }
    }

    // In this section you declare where to find the dependencies of your project
    repositories {
        mavenCentral()
        maven {
            credentials {
                username nexusUser
                password nexusPassword
            }
            url "https://mvn.elab.warwick.ac.uk/nexus/content/groups/public"
        }
        maven { url "http://oauth.googlecode.com/svn/code/maven" }
        maven { url 'http://repo.spring.io/snapshot' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "https://repository.apache.org/content/repositories/snapshots" }
        maven { url "http://dl.bintray.com/scalaz/releases" }
        mavenLocal()
    }

    configurations {
        all {
            exclude group: 'xerces', module: 'xerces'
            exclude group: 'javax.servlet', module: 'servletapi'
            exclude group: 'servletapi', module: 'servletapi'
            exclude group: 'log4j', module: 'log4j'
            exclude group: 'jboss', module: 'javassist'

            resolutionStrategy {
                // deal with transitive dep relocation
                force 'xml-apis:xml-apis:1.4.01'
            }
        }
    }

    dependencies {
        compile "org.scala-lang:scala-library:${scalaVersion}"
        compile "com.jsuereth:scala-arm_${scalaMajorVersion}:1.4"

        // 'providedCompile' deps are not included in WAR and also makes it compile within IDEA internally.
        compileOnly 'javax.servlet:javax.servlet-api:3.1.0'
        compile 'javax.servlet.jsp:javax.servlet.jsp-api:2.3.1'
        compile 'javax.servlet:jstl:1.2'

        /* LOGGING */
        compileOnly 'uk.ac.warwick:warwick-logging:1.0:all'

        // Provided by the above
        compileOnly 'ch.qos.logback:logback-classic:1.2.3'
        compileOnly('net.logstash.logback:logstash-logback-encoder:4.11') {
            exclude group: 'com.fasterxml.jackson.core'
        }
        compileOnly 'org.slf4j:slf4j-api:1.7.25'

        compile 'org.slf4j:log4j-over-slf4j:1.7.25'
        compile 'org.slf4j:jcl-over-slf4j:1.7.25'
        compile 'org.slf4j:jul-to-slf4j:1.7.25'
        // Not actually provided -  it's a ruse to exclude commons-logging from the WAR.
        // The API at runtime is provided by jcl-over-slf4j.
        compileOnly 'commons-logging:commons-logging:1.2'
        /* ******* */

        testCompile 'junit:junit:4.12'
        testCompile 'org.jmock:jmock-junit4:2.5.1'
        testCompile 'jmock:jmock-cglib:1.2.0'
        testCompile "org.specs2:specs2_${scalaMajorVersion}:2.4.15"
        testCompile "org.scalatest:scalatest_${scalaMajorVersion}:2.2.4"
        testCompile 'org.mockito:mockito-all:1.10.19'
    }

    tasks.withType(ScalaCompile) {
        configure(scalaCompileOptions.forkOptions) {
            memoryMaximumSize = '2048m'
        }
    }

    test {
        def forks = Runtime.runtime.availableProcessors() / 2
        doFirst {
            println "Executing tests with ${forks} forks"
        }
        ignoreFailures = true // So we can quarantine in Bamboo
        maxHeapSize = '2048m'
        maxParallelForks = forks
        systemProperties = [TestProcessId: "F1"] // FIXME
        testLogging {
            // set options for log level LIFECYCLE
            events "failed"
            exceptionFormat "short"
            // set options for log level DEBUG
            debug {
                events "started", "skipped", "failed"
                exceptionFormat "full"
            }
        }
        afterTest { desc, result ->
            println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
        }
    }

    tasks.withType(War) {
        dependsOn(generateRebel)
    }
}

// upgrade to a different version of Gradle by changing this
// value then running `./gradlew wrapper`. Next time gradlew
// is run it will fetch and use that version.
task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
    distributionType = 'ALL'
}
